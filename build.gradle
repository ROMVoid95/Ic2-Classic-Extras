buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url "http://repo.spongepowered.org/maven" }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            name = "jitpack"
            setUrl("https://jitpack.io")
        }
    }
    dependencies {
        classpath("com.github.GregTechCE:ForgeGradle:FG_2.3-SNAPSHOT") {
            changing = true
        }
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "${mod_version}"
group = "trinsdar.ic2c_extras" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "IC2CExtras"



sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.


compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${forgemc_version}-${forge_version}"
    runDir = "run"
    mappings = "${mappings_version}"
    coreMod = "trinsdar.ic2c_extras.asm.Ic2cExtrasLoadingPlugin"

    replace "@VERSION@", project.version
    replaceIn "IC2CExtras.java"

}

repositories{
    maven { url "https://repo.spongepowered.org/repository/maven-public" }
    maven { // JEI
        name 'DVS1 Maven FS'
        url 'https://dvs1.progwml6.com/files/maven/'
    }
    maven { // The One Probe
        name 'tterrag'
        url 'https://maven.tterrag.com/'
    }
    maven { // Tesla
        name 'MMD'
        url 'https://maven.mcmoddev.com/'
    }
    maven {
        name = "jared"
        url = "https://maven.blamejared.com"
    }
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "buildcraft"
        url = "https://www.mod-buildcraft.com/maven/"
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:19.0.0'
    compile("org.spongepowered:mixin:0.8-SNAPSHOT"){
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "log4j-core"
        exclude module: "gson"
        exclude module: "commons-io"
    }
    deobfProvided "mezz.jei:jei_${forgemc_version}:${jei_version}:api"
    runtime "mezz.jei:jei_${forgemc_version}:${jei_version}"
    deobfProvided "curse.maven:basemetals-240967:2712965"
    deobfProvided "curse.maven:modernmetals-244357:2712959"
    deobfProvided "curse.maven:orespawn-245586:2913786"
    //one probe
    deobfProvided "mcjty.theoneprobe:TheOneProbe-${mc_version}:${mc_version}-${top_version}"
    runtime "mcjty.theoneprobe:TheOneProbe-${mc_version}:${mc_version}-${top_version}"
    deobfCompile(group: 'CraftTweaker2', name: 'ZenScript', version: craft_tweaker_version) {
        exclude group: 'org.ow2.asm'
        exclude group: 'com.google.code.gson'
    }
    deobfCompile(group: 'CraftTweaker2', name: 'CraftTweaker2-API', version: craft_tweaker_version) {
        exclude group: 'org.ow2.asm'
        exclude group: 'com.google.code.gson'
    }
    deobfCompile(group: 'CraftTweaker2', name: 'CraftTweaker2-MC1120-Main', version: "1.12-${craft_tweaker_version}") {
        exclude group: 'org.ow2.asm'
        exclude group: 'com.google.code.gson'
    }

    deobfCompile("com.mod-buildcraft:buildcraft-main:7.99.24.1")

    //baubles
    compileOnly "curse.maven:gtclassic-324021:3475248"
    compile "curse.maven:baubles-227083:2518667"
    compileOnly "curse.maven:railcraft-51195:2687757"
    deobfCompile "curse.maven:ic2_classic-242942:4048720"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "modversion", "${mod_version}"
    inputs.property "mcversion", "${mc_version}"

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'modversion':"${mod_version}", 'mcversion':"${mc_version}"
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    add sourceSets.main, "mixins.ic2c_extras.refmap.json"
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

jar {
    classifier "thin"
    manifest.attributes(
            "FMLCorePlugin": "trinsdar.ic2c_extras.asm.Ic2cExtrasLoadingPlugin",
            "FMLCorePluginContainsFMLMod": true,
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            'ForceLoadAsMod': true,
            "TweakOrder": 0,
            "MixinConfigs": "mixins.ic2c_extras.json"
    )
}

shadowJar {
    classifier ""
    exclude "LICENSE.txt", "dummyThing"
    dependencies {
        include dependency('org.spongepowered:mixin')
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest.attributes(
            "FMLCorePlugin": "trinsdar.ic2c_extras.asm.Ic2cExtrasLoadingPlugin",
            "FMLCorePluginContainsFMLMod": true,
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": 0,
            "MixinConfigs": "mixins.ic2c_extras.json"
    )
}

artifacts {
    archives jar
    archives shadowJar
    archives deobfJar
}
